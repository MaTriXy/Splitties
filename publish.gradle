/*
 * Copyright (c) 2018. Louis Cognault Ayeva Derman
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

if (isRelease) apply from: '../no-version-ranges.gradle'

apply plugin: 'com.github.dcendents.android-maven'

group = 'com.louiscad.splitties'
project.archivesBaseName = "splitties-${project.name}"
version = library_version
def gitUrl = 'https://github.com/LouisCAD/Splitties.git'
def siteUrl = 'https://github.com/LouisCAD/Splitties'
def libraryDesc = 'A collection of light, general purpose Android libraries in Kotlin.'

// TODO replace with https://issuetracker.google.com/issues/72050365 once released.
android.libraryVariants.all {
    it.generateBuildConfig.enabled = false
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

install {
    group = 'publishing'
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom.project {
            packaging 'aar'

            name "Splitties"
            description libraryDesc
            url siteUrl

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'louiscad'
                    name 'Louis CAD'
                    email 'louis.cognault@gmail.com'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

if (isSnapshot) {
    apply plugin: 'com.jfrog.artifactory'
    artifactoryPublish.doFirst {
        def runFromIde = System.getProperties()['idea.platform.prefix'] != null
        if (runFromIde) {
            def command = "./gradlew artifactoryPublish"
            def errorMsg = "artifactoryPublish doesn\'t work from IDE"
            throw new IllegalStateException("$errorMsg. Please run \"$command\" from command line.")
        }
    }
    artifactoryPublish.dependsOn install
    artifactory {
        contextUrl = 'https://oss.jfrog.org/artifactory'
        publish {
            repository {
                repoKey = 'oss-snapshot-local'
                username = bintray_user
                password = bintray_api_key
                maven = true
            }
            defaults {
                publishConfigs('archives')
                publishArtifacts = true
                publishPom = true
                publishIvy = false
            }
        }
    }
} else {
    assert isRelease
    apply plugin: 'com.jfrog.bintray'
    bintrayUpload.doFirst {
        def gitTag = 'git describe --dirty'.execute().text.trim()
        def expectedTag = "v$library_version"
        if (gitTag != expectedTag) {
            throw new IllegalStateException("Expected git tag '$expectedTag' but got '$gitTag'")
        }
    }
    bintrayUpload.dependsOn install
    bintray {
        user = bintray_user
        key = bintray_api_key
        configurations = ['archives']
        override = true
        pkg {
            repo = 'maven'
            name = "splitties"
            desc = libraryDesc
            websiteUrl = siteUrl
            issueTrackerUrl = 'https://github.com/LouisCAD/Splitties/issues'
            vcsUrl = gitUrl
            licenses = ['Apache-2.0']
            labels = ['aar', 'android', 'kotlin']
            publicDownloadNumbers = true
            githubRepo = 'LouisCAD/Splitties'
        }
    }
}
